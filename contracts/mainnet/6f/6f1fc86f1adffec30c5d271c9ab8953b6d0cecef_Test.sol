/**
 *Submitted for verification at FtmScan.com on 2022-04-22
*/

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

// Sources flattened with hardhat v2.8.3 https://hardhat.org

// File @openzeppelin/contracts/proxy/Clones.sol

// OpenZeppelin Contracts v4.4.1 (proxy/Clones.sol)

/**
 * @dev https://eips.ethereum.org/EIPS/eip-1167[EIP 1167] is a standard for
 * deploying minimal proxy contracts, also known as "clones".
 *
 * > To simply and cheaply clone contract functionality in an immutable way, this standard specifies
 * > a minimal bytecode implementation that delegates all calls to a known, fixed address.
 *
 * The library includes functions to deploy a proxy using either `create` (traditional deployment) or `create2`
 * (salted deterministic deployment). It also includes functions to predict the addresses of clones deployed using the
 * deterministic method.
 *
 * _Available since v3.4._
 */
library Clones {
    /**
     * @dev Deploys and returns the address of a clone that mimics the behaviour of `implementation`.
     *
     * This function uses the create opcode, which should never revert.
     */
    function clone(address implementation) internal returns (address instance) {
        assembly {
            let ptr := mload(0x40)
            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000000000000000000000)
            mstore(add(ptr, 0x14), shl(0x60, implementation))
            mstore(add(ptr, 0x28), 0x5af43d82803e903d91602b57fd5bf30000000000000000000000000000000000)
            instance := create(0, ptr, 0x37)
        }
        require(instance != address(0), "ERC1167: create failed");
    }

    /**
     * @dev Deploys and returns the address of a clone that mimics the behaviour of `implementation`.
     *
     * This function uses the create2 opcode and a `salt` to deterministically deploy
     * the clone. Using the same `implementation` and `salt` multiple time will revert, since
     * the clones cannot be deployed twice at the same address.
     */
    function cloneDeterministic(address implementation, bytes32 salt) internal returns (address instance) {
        assembly {
            let ptr := mload(0x40)
            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000000000000000000000)
            mstore(add(ptr, 0x14), shl(0x60, implementation))
            mstore(add(ptr, 0x28), 0x5af43d82803e903d91602b57fd5bf30000000000000000000000000000000000)
            instance := create2(0, ptr, 0x37, salt)
        }
        require(instance != address(0), "ERC1167: create2 failed");
    }

    /**
     * @dev Computes the address of a clone deployed using {Clones-cloneDeterministic}.
     */
    function predictDeterministicAddress(
        address implementation,
        bytes32 salt,
        address deployer
    ) internal pure returns (address predicted) {
        assembly {
            let ptr := mload(0x40)
            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000000000000000000000)
            mstore(add(ptr, 0x14), shl(0x60, implementation))
            mstore(add(ptr, 0x28), 0x5af43d82803e903d91602b57fd5bf3ff00000000000000000000000000000000)
            mstore(add(ptr, 0x38), shl(0x60, deployer))
            mstore(add(ptr, 0x4c), salt)
            mstore(add(ptr, 0x6c), keccak256(ptr, 0x37))
            predicted := keccak256(add(ptr, 0x37), 0x55)
        }
    }

    /**
     * @dev Computes the address of a clone deployed using {Clones-cloneDeterministic}.
     */
    function predictDeterministicAddress(address implementation, bytes32 salt)
        internal
        view
        returns (address predicted)
    {
        return predictDeterministicAddress(implementation, salt, address(this));
    }
}

// File contracts/interfaces/ICloneable.sol

interface ICloneable {
    function clone() external returns (address);

    function cloneDeterministic(bytes32 salt) external returns (address);

    function isClone() external view returns (bool);

    function parent() external view returns (address);

    function predictDeterministicAddress(bytes32 salt, address deployer) external view returns (address);

    function predictDeterministicAddress(bytes32 salt) external view returns (address);
}

// File contracts/Cloneable/Cloneable.sol

abstract contract Cloneable is ICloneable {
    using Clones for address;

    event ContractCloneCreated(address indexed parent, address indexed clone, bytes32 indexed salt);

    /**
     * @dev Deploys and returns the address of a clone that mimics the behaviour of `implementation`.
     *
     * This function uses the create opcode, which should never revert.
     */
    function clone() public returns (address instance) {
        if (address(this).code.length > 45) {
            instance = address(this).clone();
            emit ContractCloneCreated(address(this), instance, 0x0);
        } else {
            instance = ICloneable(parent()).clone();
            emit ContractCloneCreated(parent(), instance, 0x0);
        }
    }

    /**
     * @dev Deploys and returns the address of a clone that mimics the behaviour of `implementation`.
     *
     * This function uses the create2 opcode and a `salt` to deterministically deploy
     * the clone. Using the same `implementation` and `salt` multiple time will revert, since
     * the clones cannot be deployed twice at the same address.
     */
    function cloneDeterministic(bytes32 salt) public returns (address instance) {
        if (address(this).code.length > 45) {
            instance = address(this).cloneDeterministic(salt);
            emit ContractCloneCreated(address(this), instance, salt);
        } else {
            instance = ICloneable(parent()).cloneDeterministic(salt);
            emit ContractCloneCreated(parent(), instance, salt);
        }
    }

    /**
     * @dev Returns if this contract is a clone
     */
    function isClone() public view returns (bool) {
        return (address(this).code.length == 45);
    }

    /**
     * @dev Returns the parent contract address or self if the parent
     */
    function parent() public view returns (address) {
        if (address(this).code.length > 45) {
            return address(this);
        } else {
            bytes memory _parent = new bytes(20);
            address master;

            uint256 k = 0;
            for (uint256 i = 10; i <= 29; i++) {
                _parent[k++] = address(this).code[i];
            }

            assembly {
                master := mload(add(_parent, 20))
            }

            return master;
        }
    }

    /**
     * @dev Computes the address of a clone deployed using {Clones-cloneDeterministic}.
     */
    function predictDeterministicAddress(bytes32 salt, address deployer) public view returns (address) {
        require(address(this).code.length > 45, "Cannot clone a clone");
        return address(this).predictDeterministicAddress(salt, deployer);
    }

    /**
     * @dev Computes the address of a clone deployed using {Clones-cloneDeterministic}.
     */
    function predictDeterministicAddress(bytes32 salt) public view returns (address) {
        require(address(this).code.length > 45, "Cannot clone a clone");
        return address(this).predictDeterministicAddress(salt);
    }
}

contract Test is Cloneable {}